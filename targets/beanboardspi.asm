;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; RA8875 simple transport layer for parallel interface
;
; Provides the low-level SPI transport interface to be used by ra8875.asm.
; Written for BeanBoard with the BeanBoardSPI expansion board which 
; has a hardware SPI interface to connect to the RA8875. 
;
; Interface (PUBLIC):
;   ra8875_reset_assert  - Assert RESET via control register
;   ra8875_reset_deassert - Deassert RESET via control register
;   ra8875_cs_start      - Assert CS with setup delay
;   ra8875_cs_end        - Deassert CS with hold delay
;   ra8875_write         - Write byte via hardware SPI
;   ra8875_read          - Read byte via hardware SPI
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

    PUBLIC ra8875_reset_assert
    PUBLIC ra8875_reset_deassert
    PUBLIC ra8875_cs_start
    PUBLIC ra8875_cs_end
    PUBLIC ra8875_write
    PUBLIC ra8875_read

    ; port numbers provided in environment.asm
    EXTERN RA8875_SPI_CTRL
    EXTERN RA8875_SPI_DATA


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; definitions
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Control register values (active low bits)
; Bit 0: RESET, Bit 1: SPI0 CS, Bits 2-7: SPI1-SPI6 CS (all need to be set high)
RA8875_SPI_IDLE     equ 0xFF       ; all deselected, reset released
RA8875_SPI_RESET    equ 0xFE       ; bit 0 low = reset asserted
RA8875_SPI_SELECT_0 equ 0xFD       ; bit 1 low = SPI0 selected

; Status register (read from same port as control register)
SPI_STAT_READY      equ 0x01       ; bit 0 (~SER_EN): high = serialisation complete


;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; transport interface
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; Assert RA8875 RESET (active low via SPI control register)
; Destroys: AF
ra8875_reset_assert:
    push af
    ld a,RA8875_SPI_RESET
    out (RA8875_SPI_CTRL),a
    pop af
    ret


; Deassert RA8875 RESET (release)
; Destroys: AF
ra8875_reset_deassert:
    push af
    ld a,RA8875_SPI_IDLE
    out (RA8875_SPI_CTRL),a
    pop af
    ret


; Assert SPI0 chip select with setup delay
; Destroys: AF
ra8875_cs_start:
    push af
    ld a,RA8875_SPI_SELECT_0
    out (RA8875_SPI_CTRL),a
    pop af
    ret


; Deassert SPI0 chip select with hold delay
; Destroys: AF
ra8875_cs_end:
    push af
    ld a,RA8875_SPI_IDLE
    out (RA8875_SPI_CTRL),a
    pop af
    ret


; Write a byte over hardware SPI
; Input: A = byte to send
; Destroys: AF
ra8875_write:
    out (RA8875_SPI_DATA),a
_ra8875_write_wait:
    in a,(RA8875_SPI_CTRL)
    bit 0,a
    jr z,_ra8875_write_wait     ; bit 0 low = serialising
    ret


; Read a byte over hardware SPI
; Sends a dummy byte (0x00) to clock in the response
; Output: A = byte received
; Destroys: AF
ra8875_read:
    ld a,0x00
    out (RA8875_SPI_DATA),a
_ra8875_read_wait:
    in a,(RA8875_SPI_CTRL)
    bit 0,a
    jr z,_ra8875_read_wait      ; bit 0 low = serialising
    in a,(RA8875_SPI_DATA)
    ret
